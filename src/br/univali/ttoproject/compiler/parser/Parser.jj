options {
	STATIC = false;
	//ERROR_REPORTING = true;
	//DEBUG_TOKEN_MANAGER = true;
}

PARSER_BEGIN(Parser)

package br.univali.ttoproject.compiler.parser;


import br.univali.ttoproject.compiler.*;
import br.univali.ttoproject.vm.*;
import java.util.ArrayList;

public class Parser {
    public String errorMessages = "";
    public ArrayList<Instruction<Integer, Object>> program = new ArrayList<>();
    private SemanticAnalysis semanticAnalysis = new SemanticAnalysis();
}

PARSER_END(Parser)

SKIP :
{
  " "
| "\t"
| "\n"
| "\r"
| <"//" (~["\n","\r"])* ("\n"|"\r"|"\r\n")>
}

SKIP: { "/*" : BLOCK_COMMENT_STATE}
<BLOCK_COMMENT_STATE> SKIP: { "*/" : DEFAULT  }
<BLOCK_COMMENT_STATE> SKIP: { < ~[] > }

<*> TOKEN :
{
    <EOF>
    {
        if (curLexState == BLOCK_COMMENT_STATE) {
            throw new Error("Unmatched comment at line " + matchedToken.beginLine + ", column " + matchedToken.beginColumn + ".");
        }
    }
}

/* KEY WORDS */
TOKEN [IGNORE_CASE]:
{
    <PROGRAM: "program">
|   <DEFINE: "define">
|   <NOT: "not">
|   <VARIABLE: "variable">
|   <IS: "is">
|   <NATURAL_TYPE: "natural">
|   <REAL_TYPE: "real">
|   <CHAR_TYPE: "char">
|   <BOOLEAN_TYPE: "boolean">
|   <EXECUTE: "execute">
|   <SET: "set">
|   <TO: "to">
|   <GET: "get">
|   <PUT: "put">
|   <VERIFY: "verify">
|   <LOOP: "loop">
|   <WHILE: "while">
|   <DO: "do">
}

/* SPECIAL SYMBOLS */
TOKEN :
{
    <HEADER: ":-">
|   <LBRACE: "{">
|   <RBRACE: "}">
|   <LBRACKET: "[">
|   <RBRACKET: "]">
|   <LPARENTHESES: "(">
|   <RPARANTHESES: ")">
|   <DOT: ".">
|   <COMMA: ",">
|   <PLUS: "+">
|   <MINUS: "-">
|   <POWER: "**">
|   <MULTIPLICATION: "*">
|   <DIVISION: "/">
|   <INTEGER_DIVISION: "%">
|   <REST: "%%">
|   <EQUAL: "==">
|   <DIFFERENT: "!=">
|   <SMALLER: "<">
|   <GREATER: ">">
|   <SMALLER_EQUAL: "<=">
|   <GREATER_EQUAL: ">=">
|   <AND: "&">
|   <OR: "|">
|   <NOT_SYMBOL: "!">
}

/* CONSTS AND IDENTIFIER */
TOKEN :
{
    <CHAR_CONST: "\"" (~["\"","\\","\n","\r"] | "\\"(["n","t","b","r","\\","'","\""]))* "\""
               | "\'" (~["\'","\\","\n","\r"] | "\\"(["n","t","b","r","\\","'","\""]))* "\'">
|   <NATURAL_CONST: <DIGIT> (<DIGIT>)? (<DIGIT>)?>
|   <REAL_CONST: <DIGIT> (<DIGIT>)? (<DIGIT>)? (<DIGIT>)? (<DIGIT>)?  "." <DIGIT> (<DIGIT>)?>
|   <TRUE: "true">
|   <FALSE: "false">
|   <IDENTIFIER: <ND>|(("_"<NU>|<LETTER>) (<NU>|"_"<NU>)* (<LETTER>|<DIGIT><ND>|<LETTER>"_"|"_"<LETTER>|"_"))>
}

/* RAW */
TOKEN :
{
    <LETTER: ["a"-"z","A"-"Z"]>
|   <DIGIT: ["0"-"9"]>
|   <NU: <LETTER>|<DIGIT><ND>>
|   <ND: <LETTER>|"_">
}

<*> TOKEN : { <UNKNOWN: ~[" ","\t","\n","\r"]> }



void Start() :
{}
{
    Program() <EOF>
    {
        program = semanticAnalysis.getProgram();
    }
}

void Program() :
{}
{
    Header() <PROGRAM> <LBRACE> Define() Execute() <RBRACE> ProgramIdentifier() /*1*/
}

void Header() :
{}
{
    <HEADER> <CHAR_CONST>
|   Epsilon()
}

void ProgramIdentifier() :
{}
{
    <IDENTIFIER> /*2*/
|   Epsilon()
}

void Define() :
{}
{
    <DEFINE> <LBRACE> DefineBlock() <RBRACE>
|   Epsilon()
}

void DefineBlock() :
{}
{
    NotVariable() VariableSel()
|   Variable() NotVariableSel()
}

void NotVariable() :
{}
{
    <NOT> <VARIABLE> /*3*/ NotVariableList()
}

void NotVariableList() :
{}
{
    Type() <IS> NotVariableIdentifierList() /*4*/ Value() /*5*/ <DOT> NotVariableListSel()
}

void NotVariableListSel() :
{}
{
    NotVariableList()
|   Epsilon()
}

void NotVariableSel() :
{}
{
    NotVariable()
|   Epsilon()
}

void Variable() :
{}
{
    <VARIABLE> /*6*/ VariableList()
}

void VariableList() :
{}
{
    Type() <IS> VariableIdentifierList() /*4*/ <DOT> VariableListSel()
}

void VariableListSel() :
{}
{
    VariableList()
|   Epsilon()
}

void VariableSel() :
{}
{
    Variable()
|   Epsilon()
}

void Type() :
{}
{
    <CHAR_TYPE> /*7*/
|   <NATURAL_TYPE> /*8*/
|   <REAL_TYPE> /*9*/
|   <BOOLEAN_TYPE> /*10*/
}

void NotVariableIdentifierList() :
{}
{
    <IDENTIFIER> /*11*/ NotVariableIdentifierListSel()
}

void NotVariableIdentifierListSel() :
{}
{
    <COMMA> NotVariableIdentifierList()
|   Epsilon()
}

void VariableIdentifierList() :
{}
{
    <IDENTIFIER> /*12*/ Index() /*13*/ VariableIdentifierListSel()
}

void VariableIdentifierListSel() :
{}
{
    <COMMA> VariableIdentifierList()
|   Epsilon()
}

void Index() :
{}
{
    <LBRACKET> <NATURAL_CONST> /*14*/ <RBRACKET>
|   Epsilon()
}

void Value() :
{}
{
    <CHAR_CONST>
|   <NATURAL_CONST>
|   <REAL_CONST>
}

void Execute() :
{}
{
    <EXECUTE> <LBRACE> CommandList() <RBRACE>
}

void CommandList() :
{}
{
    Command() <DOT> CommandListSel()
}

void CommandListSel() :
{}
{
    CommandList()
|   Epsilon()
}

void Command() :
{}
{
    Set()
|   Get()
|   Put()
|   Verify()
|   Loop()
}

void Set() :
{}
{
    <SET> /*15*/ Expression() <TO> VariableIdentifierList() /*16*/
}

void Get() :
{}
{
    <GET> /*17*/ <LBRACE> VariableIdentifierList() <RBRACE>
}

void Put() :
{}
{
    <PUT> <LBRACE> IdentifierListOrConstants() <RBRACE>
}

void IdentifierListOrConstants() :
{}
{
    Item() /*18*/ IdentifierListOrConstsSel()
}

void IdentifierListOrConstsSel() :
{}
{
    <COMMA> IdentifierListOrConstants()
|   Epsilon()
}

void Item() :
{}
{
    <IDENTIFIER> /*19*/ Index() /*20*/
|   <NATURAL_CONST> /*21*/
|   <REAL_CONST> /*22*/
|   <CHAR_CONST> /*23*/
}

void Verify() :
{}
{
    <VERIFY> Expression() <IS> Clauses() /*24*/
}

void Clauses() :
{}
{
    <TRUE> /*25*/ <LBRACE> CommandList() <RBRACE> IsFalse()
|   <FALSE> /*26*/ <LBRACE> CommandList() <RBRACE> IsTrue()
}

void IsTrue() :
{}
{
    <IS> <TRUE> /*27*/ <LBRACE> CommandList() <RBRACE>
|   Epsilon()
}

void IsFalse() :
{}
{
    <IS> <FALSE> /*27*/ <LBRACE> CommandList() <RBRACE>
|   Epsilon()
}

void Loop() :
{}
{
    <LOOP> /*28*/ <LBRACE> CommandList() <RBRACE> <WHILE> Expression() /*29*/ <IS> <TRUE>
|   <WHILE> /*30*/ Expression() /*31*/ <IS> <TRUE> <DO> <LBRACE> CommandList() <RBRACE> /*32*/
}

void Expression() :
{}
{
    ExprArithLogic() ExpressionSel()
}

void ExpressionSel() :
{}
{
    <EQUAL> ExprArithLogic() /*33*/
|   <DIFFERENT> ExprArithLogic() /*34*/
|   <SMALLER> ExprArithLogic() /*35*/
|   <GREATER> ExprArithLogic() /*36*/
|   <SMALLER_EQUAL> ExprArithLogic() /*37*/
|   <GREATER_EQUAL> ExprArithLogic() /*38*/
|   Epsilon()
}

void ExprArithLogic() :
{}
{
    Term2() LessPriority()
}

void LessPriority() :
{}
{
    <PLUS> Term2() LessPriority() /*39*/
|   <MINUS> Term2() LessPriority() /*40*/
|   <OR> Term2() LessPriority() /*41*/
|   Epsilon()
}

void Term2() :
{}
{
    Term1() MidPriority()
}

void MidPriority() :
{}
{
    <MULTIPLICATION> Term1() MidPriority() /*42*/
|   <DIVISION> Term1() MidPriority() /*43*/
|   <INTEGER_DIVISION> Term1() MidPriority() /*44*/
|   <REST> Term1() MidPriority() /*45*/
|   <AND> Term1() MidPriority() /*46*/
|   Epsilon()
}

void Term1() :
{}
{
    Element() GreatPriority()
}

void GreatPriority() :
{}
{
    <POWER> Element() GreatPriority() /*48*/
|   Epsilon()
}

void Element() :
{}
{
    <IDENTIFIER> /*19*/ Index() /*20*/
|   <NATURAL_CONST> /*21*/
|   <REAL_CONST> /*22*/
|   <CHAR_CONST> /*23*/
|   <TRUE> /*48*/
|   <FALSE> /*49*/
|   <LPARENTHESES> Expression() <RPARANTHESES>
|   <NOT> <LPARENTHESES> Expression() <RPARANTHESES> /*50*/
}

void Epsilon() : {}
{
     {}
}

