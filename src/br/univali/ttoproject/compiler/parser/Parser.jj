options {
	STATIC = false;
	//ERROR_REPORTING = true;
	//DEBUG_TOKEN_MANAGER = true;
}

PARSER_BEGIN(Parser)

package br.univali.ttoproject.compiler.parser;

public class Parser {
    public String errorMessages = "";
}

PARSER_END(Parser)

SKIP :
{
  " "
| "\t"
| "\n"
| "\r"
| <"//" (~["\n","\r"])* ("\n"|"\r"|"\r\n")>
}

SKIP: { "/*" : BLOCK_COMMENT_STATE}
<BLOCK_COMMENT_STATE> SKIP: { "*/" : DEFAULT  }
<BLOCK_COMMENT_STATE> SKIP: { < ~[] > }

<*> TOKEN :
{
    <EOF>
    {
        if (curLexState == BLOCK_COMMENT_STATE) {
            throw new Error("Unmatched comment at line " + matchedToken.beginLine + ", column " + matchedToken.beginColumn + ".");
        }
    }
}

/* KEY WORDS */
TOKEN [IGNORE_CASE]:
{
    <PROGRAM: "program">
|   <DEFINE: "define">
|   <NOT: "not">
|   <VARIABLE: "variable">
|   <IS: "is">
|   <NATURAL_TYPE: "natural">
|   <REAL_TYPE: "real">
|   <CHAR_TYPE: "char">
|   <BOOLEAN_TYPE: "boolean">
|   <EXECUTE: "execute">
|   <SET: "set">
|   <TO: "to">
|   <GET: "get">
|   <PUT: "put">
|   <VERIFY: "verify">
|   <LOOP: "loop">
|   <WHILE: "while">
|   <DO: "do">
}

/* SPECIAL SYMBOLS */
TOKEN :
{
    <HEADER: ":-">
|   <LBRACE: "{">
|   <RBRACE: "}">
|   <LBRACKET: "[">
|   <RBRACKET: "]">
|   <LPARENTHESES: "(">
|   <RPARANTHESES: ")">
|   <DOT: ".">
|   <COMMA: ",">
|   <PLUS: "+">
|   <MINUS: "-">
|   <POWER: "**">
|   <MULTIPLICATION: "*">
|   <DIVISION: "/">
|   <INTEGER_DIVISION: "%">
|   <REST: "%%">
|   <EQUAL: "==">
|   <DIFFERENT: "!=">
|   <SMALLER: "<">
|   <GREATER: ">">
|   <SMALLER_EQUAL: "<=">
|   <GREATER_EQUAL: ">=">
|   <AND: "&">
|   <OR: "|">
|   <NOT_SYMBOL: "!">
}

/* CONSTS */
TOKEN :
{
    <CHAR_CONST: "\"" (~["\"","\\","\n","\r"] | "\\"(["n","t","b","r","\\","'","\""]))* "\""
               | "\'" (~["\'","\\","\n","\r"] | "\\"(["n","t","b","r","\\","'","\""]))* "\'">
|   <NATURAL_CONST: <DIGIT> (<DIGIT>)? (<DIGIT>)?>
|   <REAL_CONST: <DIGIT> (<DIGIT>)? (<DIGIT>)? (<DIGIT>)? (<DIGIT>)?  "." <DIGIT> (<DIGIT>)?>
|   <BOOLEAN_CONST: <TRUE> | <FALSE>>
}

/* RAW */
TOKEN :
{
    <LETTER: ["a"-"z","A"-"Z"]>
|   <DIGIT: ["0"-"9"]>
|   <TRUE: "true">
|   <FALSE: "false">
}

/* LITERALS */
TOKEN :
{
    <HEADER_COMMENT: <HEADER> (~["\n","\r"])* ("\n"|"\r"|"\r\n")>
|   <IDENTIFIER: ("_"|<LETTER>)|(("_"<NU>|<LETTER>)(<NU>|"_"<NU>)* (<LETTER>|<DIGIT>("_"|<LETTER>)|<LETTER>"_"))>
|   <NU: <LETTER>|<DIGIT><ND>>
|   <ND: <LETTER>|"_">
}

<*> TOKEN : { <UNKNOWN: ~[" ","\t","\n","\r"]> }



void Start() :
{}
{
    try {
        Program() <EOF>
    } catch(ParseException e) {

    }
}

JAVACODE
void T(int tk) {
    token = getNextToken();
    //System.out.println("cur tk: " + token.image + " " + token.kind);

    while (token != null && token.kind != tk && token.kind != EOF) {
        errorMessages += "Unexpected token '" + token.image + "' at line " + token.beginLine + ", column " + token.beginColumn + "\n";

        //System.out.println("[while] cur tk: " + token.image + " " + token.kind);

        token = getNextToken();
    }

    if (token.kind == EOF) {
        throw new ParseException();
    }
}

void Program() :
{}
{
    HeaderSel() T(PROGRAM) T(LBRACE) Define() Execute() T(RBRACE) IdentifierSel()
}

void HeaderSel() :
{}
{
    <HEADER_COMMENT>
|   Epsilon()
}

void IdentifierSel() :
{}
{
    <IDENTIFIER>
|   Epsilon()
}

void Define() :
{}
{
    <DEFINE> T(LBRACE) VariableBlock() T(RBRACE)
|   Epsilon()
}

void VariableBlock() :
{}
{
    NotVariable() VariableSel1()
|   Variable() VariableSel2()
|   Epsilon()
}

void VariableSel1() :
{}
{
    Variable()
|   Epsilon()
}

void VariableSel2() :
{}
{
    NotVariable()
|   Epsilon()
}

void Execute() :
{}
{
    try {
        <EXECUTE> CommandBlock()
    } catch (ParseException e) {
        errorMessages += "Unexpected token '" + token.image + "' at line " + token.beginLine + ", column " + token.beginColumn + ". "
                       + "Expected: 'execute ...'.\n";
        token = getNextToken();
    }
}

void NotVariable() :
{}
{
    try {
        <NOT> T(VARIABLE) NotVariableDecl() NotVariableSel()
    } catch (ParseException e) {
        errorMessages += "Unexpected token '" + token.image + "' at line " + token.beginLine + ", column " + token.beginColumn + ". "
                       + "Expected: 'not variable ...'.\n";
        token = getNextToken();
    }
}

void NotVariableSel() :
{}
{
    NotVariableDecl() NotVariableSel()
|   Epsilon()
}

void NotVariableDecl() :
{}
{
    Type() T(IS) IdentifierListValue()
}

void IdentifierListValue() :
{}
{
    try {
        <IDENTIFIER> Value() IdentifierListValue1()
    } catch (ParseException e) {
        errorMessages += "Unexpected token '" + token.image + "' at line " + token.beginLine + ", column " + token.beginColumn + ". "
                       + "Expected an identifier.\n";
        token = getNextToken();
    }
}

void IdentifierListValue1() :
{}
{
    <COMMA> IdentifierListValue()
|   Epsilon()
}

void Variable() :
{}
{
    try {
        <VARIABLE> VariableDecl() VariableSel()
    } catch (ParseException e) {
        errorMessages += "Unexpected token '" + token.image + "' at line " + token.beginLine + ", column " + token.beginColumn + ". "
                       + "Expected: 'variable ...'.\n";
        token = getNextToken();
    }
}

void VariableSel() :
{}
{
    VariableDecl() VariableSel()
|   Epsilon()
}

void VariableDecl() :
{}
{
    Type() T(IS) IdentifierList() T(DOT)
}

void IdentifierList() :
{}
{
    try {
        <IDENTIFIER> Index() IdentifierList1()
    } catch (ParseException e) {
        errorMessages += "Unexpected token '" + token.image + "' at line " + token.beginLine + ", column " + token.beginColumn + ". "
                       + "Expected an identifier.\n";
        token = getNextToken();
    }
}

void IdentifierList1() :
{}
{
    <COMMA> IdentifierList()
|   Epsilon()
}

void Set() :
{}
{
    try {
        <SET> Expression() T(TO) IdentifierList()
    } catch (ParseException e) {
        errorMessages += "Unexpected token '" + token.image + "' at line " + token.beginLine + ", column " + token.beginColumn + ". "
                       + "Expected: 'set ...'.\n";
        token = getNextToken();
    }
}

void Get() :
{}
{
    try {
        <GET> T(LBRACE) IdentifierList() T(RBRACE)
    } catch (ParseException e) {
        errorMessages += "Unexpected token '" + token.image + "' at line " + token.beginLine + ", column " + token.beginColumn + ". "
                       + "Expected: 'get ...'.\n";
        token = getNextToken();
    }
}

void Put() :
{}
{
    try {
        <PUT> T(LBRACE) PutList() T(RBRACE)
    } catch (ParseException e) {
        errorMessages += "Unexpected token '" + token.image + "' at line " + token.beginLine + ", column " + token.beginColumn + ". "
                       + "Expected: 'put ...'.\n";
        token = getNextToken();
    }
}

void PutList() :
{}
{
    try {
        IdentifierList()
    |   Value()
    } catch (ParseException e) {
        errorMessages += "Unexpected token '" + token.image + "' at line " + token.beginLine + ", column " + token.beginColumn + ". "
                       + "Expected an identifier or a constant.\n";
        token = getNextToken();
    }
}

void Verify() :
{}
{
    try {
        <VERIFY> Expression() T(IS) FalseTrueSel()
    } catch (ParseException e) {
        errorMessages += "Unexpected token '" + token.image + "' at line " + token.beginLine + ", column " + token.beginColumn + ". "
                       + "Expected: 'verify ...'.\n";
        token = getNextToken();
    }
}

void FalseTrueSel() :
{}
{
    TrueBlock() FalseTrueSel1()
|   FalseBlock() FalseTrueSel2()
}

void FalseTrueSel1() :
{}
{
    FalseBlock()
|   Epsilon()
}

void FalseTrueSel2() :
{}
{
    TrueBlock()
|   Epsilon()
}

void TrueBlock() :
{}
{
    try {
        <TRUE> CommandBlock()
    } catch (ParseException e) {
        errorMessages += "Unexpected token '" + token.image + "' at line " + token.beginLine + ", column " + token.beginColumn + ". "
                       + "Expected: 'true ...'.\n";
        token = getNextToken();
    }
}

void FalseBlock() :
{}
{
    try {
        <FALSE> CommandBlock()
    } catch (ParseException e) {
        errorMessages += "Unexpected token '" + token.image + "' at line " + token.beginLine + ", column " + token.beginColumn + ". "
                       + "Expected: 'false ...'.\n";
        token = getNextToken();
    }
}

void Loop() :
{}
{
    try {
        <LOOP> CommandBlock() T(WHILE) Expression() T(IS) T(TRUE)
    } catch (ParseException e) {
        errorMessages += "Unexpected token '" + token.image + "' at line " + token.beginLine + ", column " + token.beginColumn + ". "
                       + "Expected: 'loop ...'.\n";
        token = getNextToken();
    }
}

void While() :
{}
{
    try {
        <WHILE> Expression() T(IS) T(TRUE) T(DO) CommandBlock()
    } catch (ParseException e) {
        errorMessages += "Unexpected token '" + token.image + "' at line " + token.beginLine + ", column " + token.beginColumn + ". "
                       + "Expected: 'while ...'.\n";
        token = getNextToken();
    }
}

void CommandBlock() :
{}
{
    try {
        <LBRACE> Command() CommandList() T(RBRACE)
    } catch (ParseException e) {
        errorMessages += "Unexpected token '" + token.image + "' at line " + token.beginLine + ", column " + token.beginColumn + ". "
                       + "Expected a command.\n";
        token = getNextToken();
    }
}

void CommandList() :
{}
{
    Command() CommandList()
|   Epsilon()
}

void Command() :
{}
{
    CommandSel() T(DOT)
}

void CommandSel() :
{}
{
    Set()
|   Get()
|   Put()
|   Verify()
|   Loop()
|   While()
}

void Expression() :
{}
{
    try {
        ExprArithLogic() ExpressionSel()
    } catch (ParseException e) {
        errorMessages += "Invalid expression encountered at line " + token.beginLine + ", column " + token.beginColumn + ".\n";
        token = getNextToken();
    }
}

void ExpressionSel() :
{}
{
    <EQUAL> ExprArithLogic()
|   <DIFFERENT> ExprArithLogic()
|   <SMALLER> ExprArithLogic()
|   <GREATER> ExprArithLogic()
|   <SMALLER_EQUAL> ExprArithLogic()
|   <GREATER_EQUAL> ExprArithLogic()
|   Epsilon()
}

void ExprArithLogic() :
{}
{
    Term2() LessPriority()
}

void LessPriority() :
{}
{
    <PLUS> Term2() LessPriority()
|   <MINUS> Term2() LessPriority()
|   <OR> Term2() LessPriority()
|   Epsilon()
}

void Term2() :
{}
{
    Term1() MidPriority()
}

void MidPriority() :
{}
{
    <MULTIPLICATION> Term1() MidPriority()
|   <DIVISION> Term1() MidPriority()
|   <INTEGER_DIVISION> Term1() MidPriority()
|   <REST> Term1() MidPriority()
|   <AND> Term1() MidPriority()
|   Epsilon()
}

void Term1() :
{}
{
    Element() GreatPriority()
}

void GreatPriority() :
{}
{
    <POWER> Element() GreatPriority()
|   Epsilon()
}

void Element() :
{}
{
    <IDENTIFIER> Index()
|   Value()
|   <LPARENTHESES> Expression() T(RPARANTHESES)
|   <NOT> T(LPARENTHESES) Expression() T(RPARANTHESES)
}

void Index() :
{}
{
    <LBRACKET> T(NATURAL_CONST) T(RBRACKET)
|   Epsilon()
}

void Value() :
{}
{
    try {
        <CHAR_CONST>
    |   <NATURAL_CONST>
    |   <REAL_CONST>
    |   <BOOLEAN_CONST>
    } catch (ParseException e) {
        errorMessages += "Unexpected token '" + token.image + "' at line " + token.beginLine + ", column " + token.beginColumn + ". "
                       + "Expected a constant.\n";
        token = getNextToken();
    }
}

void Type() :
{}
{
    try {
        <CHAR_TYPE>
    |   <NATURAL_TYPE>
    |   <REAL_TYPE>
    |   <BOOLEAN_TYPE>
    } catch (ParseException e) {
        errorMessages += "Unexpected token '" + token.image + "' at line " + token.beginLine + ", column " + token.beginColumn + ". "
                       + "Expected: 'char', 'natural', 'real' or 'boolean'.\n";
        token = getNextToken();
    }
}

void Epsilon() : {}
{
     {}
}
